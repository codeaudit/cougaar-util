#!/bin/csh -f

# <copyright>
#  Copyright 2001,2002 BBNT Solutions, LLC
#  under sponsorship of the Defense Advanced Research Projects Agency (DARPA).
# 
#  This program is free software; you can redistribute it and/or modify
#  it under the terms of the Cougaar Open Source License as published by
#  DARPA on the Cougaar Open Source Website (www.cougaar.org).
# 
#  THE COUGAAR SOFTWARE AND ANY DERIVATIVE SUPPLIED BY LICENSOR IS
#  PROVIDED 'AS IS' WITHOUT WARRANTIES OF ANY KIND, WHETHER EXPRESS OR
#  IMPLIED, INCLUDING (BUT NOT LIMITED TO) ALL IMPLIED WARRANTIES OF
#  MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE, AND WITHOUT
#  ANY WARRANTIES AS TO NON-INFRINGEMENT.  IN NO EVENT SHALL COPYRIGHT
#  HOLDER BE LIABLE FOR ANY DIRECT, SPECIAL, INDIRECT OR CONSEQUENTIAL
#  DAMAGES WHATSOEVER RESULTING FROM LOSS OF USE OF DATA OR PROFITS,
#  TORTIOUS CONDUCT, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
#  PERFORMANCE OF THE COUGAAR SOFTWARE.
# </copyright>


# Environment variables
# COUGAAR_INSTALL_PATH = the head of the alp install.
# COUGAAR3RDPARTY = a directory containing 3rd party jar files
#
# COUGAAR bootstrapping classpath will be:
#  $COUGAAR_DEV_PATH	if defined
#  $COUGAAR_INSTALL_PATH/lib/core.jar
#
# once running, jar files will be searched for in (in order):
#  -Dorg.cougaar.class.path 	like classpath
#  $CLASSPATH		(alp bootstrapping path from above)
#  $COUGAAR_INSTALL_PATH/lib/*
#  $COUGAAR_INSTALL_PATH/plugins/*
#  -Dorg.cougaar.system.path=$COUGAAR3RDPARTY
#  $COUGAAR_INSTALL_PATH/sys/*
#

if (! $?COUGAAR_INSTALL_PATH) then
    echo "COUGAAR_INSTALL_PATH not set!"
    exit
endif

# set COUGAAR_INSTALL_PATH=/alpine/demo98
if (! $?COUGAAR3RDPARTY) then
    setenv COUGAAR3RDPARTY /opt/cougaar-jars
endif

if (! $?COUGAAR_WORKSPACE) then
    echo "Defaulting COUGAAR_WORKSPACE to CIP/workspace"
    setenv COUGAAR_WORKSPACE ${COUGAAR_INSTALL_PATH}/workspace
endif

set LIBPATHS=$COUGAAR_INSTALL_PATH/lib/bootstrap.jar

# Optional environment variable COUGAAR_DEV_PATH can be
# used to point to custom developer code: one or more colon separated
# directories/jars/zips. It may also be left unset.

set devp=""
if ($?COUGAAR_DEV_PATH) then
    set devp="-Dorg.cougaar.class.path=${COUGAAR_DEV_PATH}"
endif

# domains are now usually defined by the config file LDMDomains.ini.
# But you may still use properties if you wish.
#set MYDOMAINS="-Dorg.cougaar.domain.alp=org.cougaar.glm.GLMDomain"
set MYDOMAINS=""
set MYPROPERTIES="$MYDOMAINS -Dorg.cougaar.system.path=$COUGAAR3RDPARTY -Dorg.cougaar.install.path=$COUGAAR_INSTALL_PATH -Duser.timezone=GMT -Dorg.cougaar.core.agent.startTime=08/10/2005 -Dorg.cougaar.planning.ldm.lps.ComplainingLP.level=0 -Dorg.cougaar.workspace=$COUGAAR_WORKSPACE"
set MYMEMORY=""

set node = $argv[1]
set rest="$argv[2-]"
if ( "$node" == "" ) then
  set node="Clusters"
endif

if ( "$node" == "admin" ) then
    set args="-c -r -n Administrator -p 8000 $rest"
    set MYMEMORY="-Djava.compiler=NONE"
else 
    set args="-n $node -c $rest"
    # arguments to adjust (defaults are given)
    # -Xmx64m	      # max java heap
    # -Xms3m	      # min (initial) java heap
    # -Xmaxf0.6       # max heap free percent
    # -Xminf0.35      # min heap free percent
    # -Xmaxe4m        # max heap expansion increment
    # -Xmine1m	      # min heap expansion increment
    # -Xoss400k       # per-thread *java* stack size
    set MYMEMORY="-Xmx768m -Xms64m -Xmaxf0.9 -Xminf0.1 -Xoss128k"
    #set MYMEMORY="-Xmx300m -Xms100m"
endif

set osargs=""
set os=`uname`
if ("$os" == "Linux") then
    # No green threads in jdk 1.3.1
    # set osargs="-green"
    # set some system runtime limits
    limit stacksize 16m    #up from 8m
    limit coredumpsize 0   #down from 1g
    #turn this on to enable inprise JIT
    #setenv JAVA_COMPILER javacomp
endif

#set javaargs="$osargs $MYPROPERTIES $MYMEMORY -classpath $LIBPATHS -Dorg.cougaar.message.isLogging=true -Djava.rmi.server.logCalls=true -Dsun.rmi.server.exceptionTrace=true -Dsun.rmi.transport.tcp.readTimeout=150000 "
set javaargs="-Xbootclasspath/p:${COUGAAR_INSTALL_PATH}/lib/javaiopatch.jar $osargs $devp $MYPROPERTIES $MYMEMORY -classpath $LIBPATHS org.cougaar.bootstrap.Bootstrapper org.cougaar.core.node.Node"
#set javaargs="$osargs $MYPROPERTIES $MYMEMORY -classpath $LIBPATHS -Dorg.cougaar.nameserver.verbosity=20"

if ($?COUGAAR_DEV_PATH) then
    echo java $javaargs $args
endif

# exec instead of eval
exec java $javaargs $args

